import dependencies.Dep
import dependencies.Packages
import dependencies.Versions

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

android {
    compileSdkVersion Versions.androidCompileSdkVersion
    defaultConfig {
        applicationId Packages.name
        minSdkVersion Versions.androidMinSdkVersion
        targetSdkVersion Versions.androidTargetSdkVersion
        versionCode Versions.androidVersionCode
        versionName Versions.androidVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        applicationIdSuffix ".compose"
    }
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release.initWith(debug)

        def releaseKeystore = file("release.keystore")

        if (isReleaseBuild) {
            assert releaseKeystore.exists()
            assert System.getenv("RELEASE_KEYSTORE_STORE_PASSWORD")
            assert System.getenv("RELEASE_KEYSTORE_KEY_ALIAS")
            assert System.getenv("RELEASE_KEYSTORE_KEY_PASSWORD")
        }

        if (releaseKeystore.exists()) {
            release {
                storeFile releaseKeystore
                storePassword System.getenv("RELEASE_KEYSTORE_STORE_PASSWORD")
                keyAlias System.getenv("RELEASE_KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("RELEASE_KEYSTORE_KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix Packages.debugNameSuffix
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose = true
    }
    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
        exclude 'META-INF/proguard/*.pro'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation project(':data:repository')
    implementation project(':data:db')
    implementation project(':data:firestore')
    implementation project(':data:api')
    implementation project(':data:device')

    implementation project(':corecomponent:androidcomponent')
    implementation project(':ext:log')

    implementation Dep.Kotlin.stdlibJvm
    implementation Dep.AndroidX.appCompat
    implementation Dep.AndroidX.coreKtx
    implementation Dep.AndroidX.constraint
    implementation Dep.AndroidX.activityKtx
    implementation Dep.Firebase.firestoreKtx
    implementation Dep.Firebase.crashlytics

    implementation Dep.AndroidX.Compose.runtime
    implementation Dep.AndroidX.Compose.core
    implementation Dep.AndroidX.Compose.layout
    implementation Dep.AndroidX.Compose.framework
    implementation Dep.AndroidX.Compose.material
    implementation Dep.AndroidX.Compose.foundation
    implementation Dep.AndroidX.Compose.text
    implementation Dep.AndroidX.Compose.tooling

    testImplementation Dep.Test.junit
    androidTestImplementation Dep.Test.testRunner
    androidTestImplementation Dep.Test.espressoCore

    debugImplementation Dep.Stetho.stetho
    debugImplementation Dep.LeakCanary.leakCanary
}

//apply plugin: 'com.google.gms.google-services'